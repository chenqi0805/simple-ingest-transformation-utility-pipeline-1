buildscript {
    ext {
        es_version = System.getProperty("es.version", "7.8.0")
        es_group = "org.elasticsearch"
        distribution = 'oss-zip'
    }

    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "${es_group}.gradle:build-tools:${es_version}"
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '6.0.0'
    id 'java'
}
version = '0.1-alpha'
group = 'com.amazon'
sourceCompatibility = '1.8'

apply plugin: 'elasticsearch.testclusters'
apply plugin: 'elasticsearch.build'
apply plugin: 'elasticsearch.rest-test'
apply plugin: 'maven-publish'

// To pass elasticsearch plugin check
ext {
    licenseFile = rootProject.file('LICENSE.TXT')
    noticeFile = rootProject.file('NOTICE.TXT')
}

repositories {
    mavenCentral()
}

dependencies {
    //apm-source
    implementation 'io.netty:netty-all:4.1.51.Final'

    // ES sink
    implementation "org.elasticsearch:elasticsearch:${es_version}"
    implementation "org.elasticsearch.client:elasticsearch-rest-client:${es_version}"

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.10.0'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.11.1'
    implementation 'org.reflections:reflections:0.9.11'
    implementation 'com.google.guava:guava:29.0-jre'
    implementation 'javax.validation:validation-api:2.0.1.Final'
    implementation 'javax.ws.rs:javax.ws.rs-api:2.1.1'
    implementation 'org.apache.bval:bval-jsr:2.0.0'
    implementation 'org.apache.bval:bval-extras:2.0.4'
    implementation 'org.slf4j:slf4j-api:1.7.30'
    implementation "org.apache.logging.log4j:log4j-core:2.11.1"
    testImplementation('junit:junit:4.13') {
        exclude group:'org.hamcrest' // workaround for jarHell
    }
    testImplementation "org.elasticsearch.test:framework:${es_version}"
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url =  "$rootDir/releases"
        }
    }
}


// Workaround for Werror
compileJava.options.warnings = false

// Resolve dependency conflict between ES sink and main project
configurations.all {
    resolutionStrategy {
        force 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.11.1'
        force 'com.fasterxml.jackson.core:jackson-databind:2.11.1'
        force 'com.fasterxml.jackson.core:jackson-core:2.11.1'
        force 'com.google.guava:guava:29.0-jre'
        force 'org.apache.bval:bval-jsr:2.0.4'
        force 'junit:junit:4.13'
    }
}

integTest {}

testClusters.integTest {
    testDistribution = "OSS"
}

integTest.runner {
    systemProperty 'tests.security.manager', 'false'
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Main-Class': 'com.amazon.ti.TransformationInstanceExecute')
    }
}

forbiddenApis.ignoreFailures = true
checkstyleMain.ignoreFailures = true
checkstyleTest.ignoreFailures = true
testingConventions.enabled = false
licenseHeaders.enabled = false
dependencyLicenses.enabled = false
thirdPartyAudit.enabled = false
validateMavenPom.enabled = false
validateShadowPom.enabled = false
validateNebulaPom.enabled = false